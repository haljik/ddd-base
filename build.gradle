plugins {
    id 'java'
    id 'idea'
    id 'org.springframework.boot' version '2.7.9'
    id "org.dddjava.jig-gradle-plugin" version "2023.2.1"
}

[compileJava, processResources]*.shouldRunAfter(clean)
jigReports.dependsOn(clean, classes)

description 'ドメイン駆動設計によるアプリケーションテンプレート'

sourceCompatibility = '11'

sourceSets {
    main {
        // mybatis SQL map XML ファイルを java 以下でも検知する
        resources.srcDirs = ["src/main/java", "src/main/resources"]
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation platform(org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES)
    implementation("org.springframework.boot:spring-boot-starter-web")
    implementation("org.springframework.boot:spring-boot-starter-thymeleaf")
    implementation("org.springframework.boot:spring-boot-starter-validation")

    implementation("org.mybatis.spring.boot:mybatis-spring-boot-starter:2.2.2")

    runtimeOnly("com.h2database:h2")

    testImplementation("org.springframework.boot:spring-boot-starter-test")
    testImplementation('org.seleniumhq.selenium:htmlunit-driver')
    testImplementation 'com.github.irof:jig-erd:0.0.16' // JDK11
}

test {
    useJUnitPlatform()
}

bootJar {
    archivesBaseName = "webapp"
}
